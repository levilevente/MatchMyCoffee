buildscript {
    repositories {
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath group: 'com.github.spotbugs.snom', name: 'spotbugs-gradle-plugin', version: '6.4.8'
        classpath group: 'se.bjurr.violations', name: 'violations-gradle-plugin', version: '4.1.2'
    }
}

apply plugin: 'checkstyle'
apply plugin: 'pmd'
apply plugin: com.github.spotbugs.snom.SpotBugsPlugin
apply plugin: se.bjurr.violations.gradle.plugin.ViolationsGradlePlugin

def configDir = buildscript.sourceFile.parentFile
def javaVersion = Integer.valueOf(JavaVersion.current().toString())

checkstyle {
    toolVersion = '10.12.4'
    ignoreFailures = true
    configFile = file("${configDir}/checkstyle.xml")
}

spotbugs {
    toolVersion = '4.8.0'
    ignoreFailures = true
    effort = com.github.spotbugs.snom.Effort.MAX
    reportLevel = com.github.spotbugs.snom.Confidence.valueOf('LOW')
    excludeFilter = file("${configDir}/spotbugs-filter.xml")
}

spotbugsMain {
    reports {
        xml.enabled = true
    }
}

pmd {
    toolVersion = javaVersion  > 17 ? '7.7.0' : '6.45.0'
    ignoreFailures = true
    ruleSetFiles = files("${configDir}/pmd-ruleset.xml")
    ruleSets = []
    rulesMinimumPriority = 5
    try {
        incrementalAnalysis = true
    } catch (def e) {
    }
}

tasks.register('violations', se.bjurr.violations.gradle.plugin.ViolationsTask) {
    maxRuleColumnWidth.set(25)
    maxMessageColumnWidth.set(80)

    minSeverity.set('INFO')
    detailLevel.set('VERBOSE')
    maxViolations.set(0)

    violations = [
            ["FINDBUGS", buildDir.path, ".*/spotbugs/.*\\.xml\$", "SpotBugs"],
            ["PMD", buildDir.path, ".*/pmd/.*\\.xml\$", "PMD"],
            ["CHECKSTYLE", buildDir.path, ".*/checkstyle/.*\\.xml\$", "Checkstyle"]
    ]
}

tasks.named('check') { finalizedBy tasks.named('violations') }